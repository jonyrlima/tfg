
[DEFNAME ARENA1X1]
LOCAL.NOME_ARENA_1			1x1/2x2 	//ARENA 1 - NOME
LOCAL_PLAYERARENA_1_1		6051,1209	//ARENA 1 - LOCAL ONDE FICA O DESAFIANTE 1
LOCAL_PLAYERARENA_1_2		6064,1200	//ARENA 1 - LOCAL ONDE FICA O DESAFIANTE 2
LOCAL_ARENA_1_EXIT_ITEM		6058,1203	//ARENA 1 - LOCAL ONDE FICA A PEDRA DE SAIDA DA ARENA X1
LOCAL.NOME_ARENA_2			Warrior		//ARENA 2 - NOME
LOCAL_PLAYERARENA_2_1		6047,1230	//ARENA 2 - LOCAL ONDE FICA O DESAFIANTE 1
LOCAL_PLAYERARENA_2_2		6054,1223	//ARENA 2 - LOCAL ONDE FICA O DESAFIANTE 2
LOCAL_ARENA_2_EXIT_ITEM		6050,1223	//ARENA 2 - LOCAL ONDE FICA A PEDRA DE SAIDA DA ARENA X1
LOCAL_SAIRARENA				5452,1194	//LOCAL PARA ONDE VAI FORA DA ARENA
TEMPO_ESPERA_OPONENTE		30			//TEMPO QUE O PLAYER ESPERA NA ARENA POR UM OPONENTE
TEMPO_LOOTIAR				300         //TEMPO PARA LOOTIAR
GOLD_ENTERX1				10000

[ITEMDEF i_ARENA_1X1]
DEFNAME=i_ARENA_1X1
NAME=Arena 1x1
ID=i_gravestone_18

ON=@CREATE
TAG.ARENA 1
TAG.NUMPLAYERS 0
TAG.ARENASTATE=
TAG.DESAFIANTES_1=
TAG.DESAFIANTES_2=
COLOR 0455
ATTR 010

ON=@CLICK
IF (<TAG.NUMPLAYERS> == 0)
	MESSAGE @35 Arena Vazia
ENDIF

IF (<TAG.NUMPLAYERS> == 1)
	MESSAGE @07ad Tempo: <TIMER>
	MESSAGE @35 <TAG.DESAFIANTES_1> X ??
ENDIF

IF (<TAG.NUMPLAYERS> == 2)
	MESSAGE @07ad Tempo Restante: <TIMER>
	MESSAGE @35 <TAG.DESAFIANTES_1> X <TAG.DESAFIANTES_2>
ENDIF
MESSAGE @455 Arena <DEF.LOCAL.NOME_ARENA_<DTAG.ARENA>>
RETURN 1

ON=@DCLICK
IF (<SRC.ISEVENT.e_weekly_pvp_single_champion>)
	TAG.COST
ELSEIF  (<serv.rtime.format %w> >= 1) && (<serv.rtime.format %w> < 5) && (<serv.rtime.format %H> >= 18) AND (<serv.rtime.format %H> <= 23)
	TAG.COST <EVAL <DEF.GOLD_ENTERX1> * 4>
ELSE
	TAG.COST <DEF.GOLD_ENTERX1>
ENDIF

IF !(<SRC.CANSEELOS <UID>>)
RETURN 1
ENDIF

IF (<SRC.TAG0.CANT_I_X1>)
	SRC.SYSMESSAGE @,,1 Voce ainda nao pode entrar nessa arena, voce desistiu de um duelo. Aguarde alguns minutos.
	RETURN 1
ENDIF

SRC.CTAG.ARENA_1X1_ITEM <UID>
SRC.MENU M_ARENAX1
return 1

ON=@TIMER
IF (<TAG0.EXIT_ARENA_ITEM>)
	TRY UID.<TAG0.EXIT_ARENA_ITEM>.REMOVE
	TAG0.EXIT_ARENA_ITEM=
ENDIF

IF (<TAG0.ARENASTATE> == 1)
	LOCAL.COUNT <SERV.LIST.ARENA_PLAYERS_<UID>_1.COUNT>

	FOR 0 <EVAL <LOCAL.COUNT> - 1>
		REF1 <SERV.LIST.ARENA_PLAYERS_<UID>_1.<DLOCAL._FOR>>

		IF (<REF1.ISEVENT.E_ARENA_1X1>)
			TRY REF1.EVENTS -E_ARENA_1X1
			TRY REF1.TAG.ARENA_1X1_ITEM
			TRY REF1.GO <DEF.LOCAL_SAIRARENA>

			REF1.KILLS=<REF1.TAG0.ARENAX1.KILLS>
			REF1.FAME=<REF1.TAG0.ARENAX1.FAME>
			REF1.KARMA=<REF1.TAG0.ARENAX1.KARMA>
			REF1.TAG0.ARENAX1.KARMA=
			REF1.TAG0.ARENAX1.FAME=
			REF1.TAG0.ARENAX1.KILLS=
			REF1.TAG0.ARENAX1.TEAM=
			REF1.CRIMINAL 0
			REF1.STONE 0
		ENDIF

		IF (<EVAL <TAG0.COST>> > 0)
			SERV.NEWITEM i_gold <EVAL <TAG.COST>>
			NEW.AMOUNT <EVAL <TAG.COST>>
			NEW.CONT <REF1.UID>
		ENDIF
	ENDFOR

	TAG.NUMPLAYERS 0
	TAG.ARENASTATE=
ENDIF

IF (<TAG0.ARENASTATE> == 2)
	LOCAL.COUNT <SERV.LIST.ARENA_PLAYERS_<UID>_1.COUNT>

	FOR 0 <EVAL <LOCAL.COUNT> - 1>
		REF1 <SERV.LIST.ARENA_PLAYERS_<UID>_1.<DLOCAL._FOR>>
		IF (<REF1.ISEVENT.E_ARENA_1X1>)
			TRY REF1.EVENTS -E_ARENA_1X1
			TRY REF1.TAG.ARENA_1X1_ITEM
			TRY REF1.GO <DEF.LOCAL_SAIRARENA>

			REF1.KILLS=<REF1.TAG0.ARENAX1.KILLS>
			REF1.FAME=<REF1.TAG0.ARENAX1.FAME>
			REF1.KARMA=<REF1.TAG0.ARENAX1.KARMA>
			REF1.TAG0.ARENAX1.KARMA=
			REF1.TAG0.ARENAX1.FAME=
			REF1.TAG0.ARENAX1.KILLS=
			REF1.TAG0.ARENAX1.TEAM=
			REF1.CRIMINAL 0
			REF1.STONE 0
		ENDIF
	ENDFOR

	LOCAL.COUNT <SERV.LIST.ARENA_PLAYERS_<UID>_2.COUNT>

	FOR 0 <EVAL <LOCAL.COUNT> - 1>
		REF1 <SERV.LIST.ARENA_PLAYERS_<UID>_2.<DLOCAL._FOR>>
		IF (<REF1.ISEVENT.E_ARENA_1X1>)
			TRY REF1.EVENTS -E_ARENA_1X1
			TRY REF1.TAG.ARENA_1X1_ITEM
			TRY REF1.GO <DEF.LOCAL_SAIRARENA>

			REF1.KILLS=<REF1.TAG0.ARENAX1.KILLS>
			REF1.FAME=<REF1.TAG0.ARENAX1.FAME>
			REF1.KARMA=<REF1.TAG0.ARENAX1.KARMA>
			REF1.TAG0.ARENAX1.KARMA=
			REF1.TAG0.ARENAX1.FAME=
			REF1.TAG0.ARENAX1.KILLS=
			REF1.TAG0.ARENAX1.TEAM=
			REF1.CRIMINAL 0
			REF1.STONE 0
		ENDIF
	ENDFOR

	REF1 <UID>

	LOCAL.ARENA_REGION_UID <SERV.MAP(<DEF.LOCAL_ARENA_<DTAG.ARENA>_EXIT_ITEM>).REGION.UID>

	FORITEMS 30
		IF (<REGION.UID> == <LOCAL.ARENA_REGION_UID>) && (<TYPE> == t_corpse)
			REMOVE
		ENDIF
	ENDFOR

	TAG.NUMPLAYERS 0
	TAG.ARENASTATE=
ENDIF
RETURN 1

[ITEMDEF i_ARENA_1X1_EXIT]
NAME=Sair da Arena
ID=i_gravestone_18

ON=@Create
	COLOR 0455
	ATTR 010

ON=@Click
	MESSAGE @,,1 Clique duas vezes para sair da arena
	RETURN 1

ON=@DClick
	IF (<SRC.FLAGS> & statf_dead)
		SRC.SYSMESSAGE @,,1 Voce estah morto !
		RETURN 1
	ENDIF

	IF !(<SRC.ISEVENT.E_ARENA_1X1>)
		SRC.SYSMESSAGE @,,1 <DEFMSG.reach_fail>
		RETURN 1
	ENDIF

	TRY UID.<SRC.TAG.ARENA_1X1_ITEM>.TIMER 3
	SRC.SYSMESSAGE @,,1 Voce sera transportado para fora da arena em 3 segundos.
	REMOVE
	RETURN 1

[FUNCTION f_remove_all_arena_players]
LOCAL.ARENA_REGION_UID <SERV.MAP(<DEF.LOCAL_ARENA_<DTAG.ARENA>_EXIT_ITEM>).REGION.UID>

FORPLAYERS 30
	LOCAL.REGION_UID <SERV.MAP(<P.X>, <P.Y>).REGION.UID>

	IF !(<ISEVENT.e_arena_invul>) && (<LOCAL.REGION_UID> == <LOCAL.ARENA_REGION_UID>)
		SERV.LOG Transporting <NAME> out of the arena.
		GO <DEF.LOCAL_SAIRARENA>
	ENDFOR
ENDFOR

[MENU M_ARENAX1]
<QVAL <EVAL <UID.<SRC.CTAG.ARENA_1X1_ITEM>.TAG0.COST>> > 0 ? Pedra da Arena <DEF.LOCAL.NOME_ARENA_<DUID.<SRC.CTAG.ARENA_1X1_ITEM>.TAG.ARENA>>. Cada oponente deve pagar <EVAL <UID.<SRC.CTAG.ARENA_1X1_ITEM>.TAG0.COST>> gp's para lutar. Deseja continuar? : Pedra da Arena <DEF.LOCAL.NOME_ARENA_<DUID.<SRC.CTAG.ARENA_1X1_ITEM>.TAG.ARENA>>. Promocao - estah de graca! Deseja continuar?>

ON=0 Sim
REF1 <UID.<SRC.CTAG.ARENA_1X1_ITEM>>

IF (<SRC.ISINPARTY>)
	IF (<SRC.PARTY.MEMBERS> > 2)
		SRC.SYSMESSAGE Voce deve estar em uma party de, no maximo, 2 jogadores.
		RETURN 1
	ENDIF

	FOR 0 <EVAL <SRC.PARTY.MEMBERS> - 1>
		REF2 <SRC.PARTY.MEMBER.<LOCAL._FOR>>

		IF !(<REF2.CANSEELOS <REF1.UID>>)
			SRC.SYSMESSAGE <REF2.NAME> nao estah perto da arena.
			RETURN 1
		ENDIF

		IF (<EVAL <REF1.TAG0.COST>> > 0) && (<REF2.GOLD> < <REF1.TAG0.COST>)
			SRC.SYSMESSAGE <REF2.NAME> deve ter <EVAL <REF1.TAG.COST>> gold.
			RETURN 1
		ENDIF

		IF (<REF2.TAG0.ARENAX1.KILLS>) || (<REF2.TAG0.ARENAX1.FAME>) || (<REF2.TAG0.ARENAX1.KARMA>)
			SRC.SYSMESSAGE Seu adversario da luta anterior ainda nao saiu da arena. Favor aguardar.
			RETURN 1
		ENDIF
	ENDFOR
ELSE
	IF (<EVAL <REF1.TAG0.COST>> > 0) && (<SRC.GOLD> < <REF1.TAG0.COST>)
		SRC.SYSMESSAGE Voce deve ter <EVAL <REF1.TAG.COST>> gold.
		RETURN 1
	ENDIF

	IF (<SRC.TAG0.ARENAX1.KILLS>) || (<SRC.TAG0.ARENAX1.FAME>) || (<SRC.TAG0.ARENAX1.KARMA>)
		SRC.SYSMESSAGE Seu adversario da luta anterior ainda nao saiu da arena. Favor aguardar.
		RETURN 1
	ENDIF
ENDIF

IF (<REF1.TAG.NUMPLAYERS> == 0)
	REF1.f_remove_all_arena_players

	SERV.LIST.ARENA_PLAYERS_<REF1.UID>_1.CLEAR

	IF (<SRC.ISINPARTY>)
		REF1.TAG.DESAFIANTES_1
		LOCAL.SEPARATOR
		FOR 0 <EVAL <SRC.PARTY.MEMBERS> - 1>
			REF2 <SRC.PARTY.MEMBER.<LOCAL._FOR>>
			SERV.LIST.ARENA_PLAYERS_<REF1.UID>_1.ADD <REF2.UID>

			IF (<LOCAL._FOR> > 0)
				REF1.TAG.DESAFIANTES_1 <REF1.TAG.DESAFIANTES_1><LOCAL.SEPARATOR><REF2.NAME>
			ELSE
				REF1.TAG.DESAFIANTES_1 <REF2.NAME>
			ENDIF

			LOCAL.SEPARATOR ", "
		ENDFOR
	ELSE
		SERV.LIST.ARENA_PLAYERS_<REF1.UID>_1.ADD <SRC.UID>

		REF1.TAG.DESAFIANTES_1 <SRC.NAME>
	ENDIF

	REF1.TAG.ARENASTATE=1
	REF1.TAG.NUMPLAYERS += 1

	LOCAL.COUNT <SERV.LIST.ARENA_PLAYERS_<REF1.UID>_1.COUNT>

	FOR 0 <EVAL <LOCAL.COUNT> - 1>
		REF2 <SERV.LIST.ARENA_PLAYERS_<REF1.UID>_1.<DLOCAL._FOR>>

		REF1.SAY <REF2.NAME> entrou na arena.
		TRY UID.<REF2.UID>.GO <DEF.LOCAL_PLAYERARENA_<DREF1.TAG.ARENA>_1>
		SERV.B @0481 <REF2.NAME> entrou na Arena <DEF.LOCAL.NOME_ARENA_<DREF1.TAG.ARENA>>
		REF2.EVENTS +E_ARENA_1X1
		REF2.TAG.ARENA_1X1_ITEM <REF1.UID>
		REF2.STONE 1
		REF1.TIMER <DEF.TEMPO_ESPERA_OPONENTE>

		REF2.TAG0.ARENAX1.KILLS=<REF2.KILLS>
		REF2.TAG0.ARENAX1.FAME=<REF2.FAME>
		REF2.TAG0.ARENAX1.KARMA=<REF2.KARMA>
		REF2.TAG0.ARENAX1.TEAM=1

		IF (<EVAL <REF1.TAG0.COST>> > 0)
			REF2.GOLD -= <REF1.TAG.COST>
		ENDIF
	ENDFOR

ELIF (<REF1.TAG.NUMPLAYERS> == 1)
	SERV.LIST.ARENA_PLAYERS_<REF1.UID>_2.CLEAR

	IF (<SRC.ISINPARTY>)
		IF (<SERV.LIST.ARENA_PLAYERS_<REF1.UID>_1.COUNT> != <SRC.PARTY.MEMBERS>)
			SRC.SYSMESSAGE A quantidade de desafiantes eh incompativel.
			RETURN 1
		ENDIF

		REF1.TAG.DESAFIANTES_2
		LOCAL.SEPARATOR
		FOR 0 <EVAL <SRC.PARTY.MEMBERS> - 1>
			REF2 <SRC.PARTY.MEMBER.<LOCAL._FOR>>
			SERV.LIST.ARENA_PLAYERS_<REF1.UID>_2.ADD <REF2.UID>

			IF (<LOCAL._FOR> > 0)
				REF1.TAG.DESAFIANTES_2 <REF1.TAG.DESAFIANTES_2><LOCAL.SEPARATOR><REF2.NAME>
			ELSE
				REF1.TAG.DESAFIANTES_2 <REF2.NAME>
			ENDIF

			LOCAL.SEPARATOR ", "
		ENDFOR
	ELSE
		IF (<SERV.LIST.ARENA_PLAYERS_<REF1.UID>_1.COUNT> != 1)
			SRC.SYSMESSAGE A quantidade de desafiantes eh incompativel.
			RETURN 1
		ENDIF

		SERV.LIST.ARENA_PLAYERS_<REF1.UID>_2.ADD <SRC.UID>

		REF1.TAG.DESAFIANTES_2 <SRC.NAME>
	ENDIF


	REF1.TAG.ARENASTATE=2
	REF1.TAG.NUMPLAYERS += 1

	LOCAL.COUNT <SERV.LIST.ARENA_PLAYERS_<REF1.UID>_2.COUNT>

	SERV.B @0481 <REF1.TAG.DESAFIANTES_2> entrou(aram) na Arena <DEF.LOCAL.NOME_ARENA_<DREF1.TAG.ARENA>> contra <REF1.TAG.DESAFIANTES_1>.

	FOR 0 <EVAL <LOCAL.COUNT> - 1>
		REF2 <SERV.LIST.ARENA_PLAYERS_<REF1.UID>_2.<DLOCAL._FOR>>
		REF3 <SERV.LIST.ARENA_PLAYERS_<REF1.UID>_1.<DLOCAL._FOR>>

		REF1.SAY <REF2.NAME> entrou na arena.
		REF2.STONE 1
		TRY UID.<REF2.UID>.GO <DEF.LOCAL_PLAYERARENA_<DREF1.TAG.ARENA>_1>
		TRY UID.<REF3.UID>.GO <DEF.LOCAL_PLAYERARENA_<DREF1.TAG.ARENA>_2>
		REF2.EVENTS +E_ARENA_1X1
		REF2.TAG.ARENA_1X1_ITEM <REF1.UID>
		REF1.TIMER 720
		TIMERF 10, UID.<REF2.UID>.STONE 0
		TIMERF 10, UID.<REF3.UID>.STONE 0
		TRY UID.<REF2.UID>.MESSAGE @,,1 A luta comeca em 10 segundos.
		TRY UID.<REF3.UID>.MESSAGE @,,1 A luta comeca em 10 segundos.

		REF2.TAG0.ARENAX1.KILLS=<REF2.KILLS>
		REF2.TAG0.ARENAX1.FAME=<REF2.FAME>
		REF2.TAG0.ARENAX1.KARMA=<REF2.KARMA>
		REF2.TAG0.ARENAX1.TEAM=2

		IF (<EVAL <REF1.TAG0.COST>> > 0)
			REF2.GOLD -= <REF1.TAG.COST>
		ENDIF
	ENDFOR

ELIF (<REF1.TAG.NUMPLAYERS> == 2)
	SRC.SYSMESSAGE @,,1 A Arena esta cheia.
ENDIF

SRC.CTAG.ARENA_1X1_ITEM

RETURN 1

ON=0 Nao
SRC.CTAG.ARENA_1X1_ITEM

RETURN 1



//////////////

[EVENTS E_ARENA_1X1]

ON=@CLICK
MESSAGE [Arena]
MESSAGE @455 <NAME>
RETURN 1

ON=@EnvironChange
	EVENTS -E_ARENA_1X1
	TAG0.ARENAX1.KARMA
	TAG0.ARENAX1.FAME
	TAG0.ARENAX1.KILLS
	TAG0.ARENAX1.TEAM
	TAG.ARENA_1X1_ITEM

ON=@SpellSelect
	IF (<SRC.FLAGS> & statf_stone)
		RETURN 1
	ENDIF

	REF1 <UID.<TAG.ARENA_1X1_ITEM>>

	IF (<ARGN1> == 59) && (<REF1.TAG0.EXIT_ARENA_ITEM>)
		SYSMESSAGE @,,1 You can't resurrect anyone here !
		RETURN 1
	ENDIF

ON=@KILL
LOCAL.PLAYER_TEAM <TAG.ARENAX1.TEAM>

IF (<ARGO.BODY> == C_MAN) || (<ARGO.BODY> == C_WOMAN)
	REF1 <UID.<TAG.ARENA_1X1_ITEM>>

	LOCAL.TEAM <ARGO.TAG.ARENAX1.TEAM>

	IF (<LOCAL.PLAYER_TEAM> == <LOCAL.TEAM>)
		SERV.LOG Ignoring team kill from arena.
	ELSE
		LOCAL.COUNT <SERV.LIST.ARENA_PLAYERS_<REF1.UID>_<DLOCAL.TEAM>.COUNT>

		LOCAL.DEAD_COUNT 1

		FOR 0 <EVAL <LOCAL.COUNT> - 1>
			REF2 <SERV.LIST.ARENA_PLAYERS_<REF1.UID>_<DLOCAL.TEAM>.<DLOCAL._FOR>>

			IF (<REF2.FLAGS> & statf_dead)
				LOCAL.DEAD_COUNT += 1
			ENDIF
		ENDFOR

		IF (<LOCAL.DEAD_COUNT> >= <LOCAL.COUNT>)
			SERV.B @0481 <REF1.TAG.DESAFIANTES_<DLOCAL.TEAM>> foi(ram) morto(s) na arena !

			IF (<BODY>=C_MAN) || (<BODY> == C_WOMAN)
				SYSMESSAGE @,,1 Voce tem <EVAL <DEF.TEMPO_LOOTIAR>> segundos para lootear seu adversario !
			ENDIF

			IF (<REF1.TAG0.EXIT_ARENA_ITEM>)
				SERV.LOG Trying to remove already existing arena exit item <REF1.TAG0.EXIT_ARENA_ITEM>.
				TRY UID.<REF1.TAG.EXIT_ARENA_ITEM>.REMOVE
			ENDIF

			SERV.LOG Creating arena exit item.

			SERV.NEWITEM i_ARENA_1X1_EXIT
			NEW.P <DEF.LOCAL_ARENA_<DREF1.TAG.ARENA>_EXIT_ITEM>

			REF1.TAG.EXIT_ARENA_ITEM <NEW.UID>
			REF1.TIMER <DEF.TEMPO_LOOTIAR>
		ENDIF
	ENDIF
ENDIF

ON=@DEATH
IF (<FINDID.I_MEM_DESLOG>)
	FINDID.I_MEM_DESLOG.TIMER <DEF.TEMPO_LOOTIAR>
ENDIF

ON=@DEATHCORPSE
LOCAL.DUID = <UID>
UID.<TAG.ARENA_1X1_ITEM>.TAG0.CORPO=<ARGO>

FORCHARS 30
	IF (<BODY> != C_MAN) && (<BODY> != C_WOMAN)
		IF (<OWNER>) && !(<TAG0.UNSHRINKABLE>)
			REF1 <OWNER.UID>

			IF (<REF1.FLAGS> & statf_dead)
				SERV.NEWITEM <ICON>
				NEW.COLOR <COLOR>
				NEW.NAME <NAME>
				NEW.MORE1 <BASEID>
				NEW.CONT <REF1.FINDLAYER.layer_bankbox.UID>

				REF1.SYSMESSAGE @,,1 Your pet (<NAME>) is shrinked inside your bank box.
			ENDIF
		ELSE
			SERV.LOG Removing <ID> / <UID> / <NAME> from arena.
		ENDIF
	ENDIF
ENDFOR

FORCHARS 30
	IF (<BODY> != C_MAN) && (<BODY> != C_WOMAN)
		IF !(<OWNER>)
			SERV.LOG Removing <ID> / <UID> / <NAME> without owner from arena.
		ELSEIF (<OWNER.FLAGS> & statf_dead)
			REMOVE
		ENDIF
	ENDIF
ENDFOR

ON=@CRIMINAL
RETURN 1

ON=@LOGOUT
SRC.SAY @455 Logout
SERV.NEWITEM i_MEM_DESLOG
NEW.EQUIP

ON=@LOGIN
IF (<FINDID.I_MEM_DESLOG>) && (<FINDID.I_MEM_DESLOG.TIMER> < 1)
	FINDID.I_MEM_DESLOG.REMOVE
ENDIF

IF (<FINDID.I_MEM_DESLOG>) && (<FINDID.I_MEM_DESLOG.TIMER> > 0)
	FINDID.I_MEM_DESLOG.REMOVE
ELSE
	SERV.NEWITEM i_MEM_TELEPORTDESLOG
	NEW.EQUIP
ENDIF

// [PLEVEL 1]
// DESISTIR_X1

[PLEVEL 4]
finalizar_arena_x1

[FUNCTION finalizar_arena_x1]
TARGETF F_FINALIZAR_X1

[FUNCTION F_FINALIZAR_X1]
IF (<ARGO.BASEID> != i_ARENA_1X1)
	SRC.SYSMESSAGE Item escolhido nao eh uma pedra de arena.
	RETURN 1
ENDIF

IF (<ARGO.TAG0.ARENASTATE> != 2)
	SRC.SYSMESSAGE @,,1 Nao existe duelo acontecendo.
	RETURN 1
endif
SRC.SYSMESSAGE @,,1 Voce finalizou o duelo. Devolva o dinheiro de <ARGO.TAG.DESAFIANTES_1> e <ARGO.TAG.DESAFIANTES_2> - <DARGO.TAG0.COST> GPs
ARGO.TIMER 1

[ITEMDEF i_MEM_DESLOG]
ID=i_MEMORY
NAME=ARENAX1 MEMORY
TYPE=T_EQ_SCRIPT

ON=@CREATE
TIMER 180

ON=@TIMER
REMOVE
RETURN 1

[ITEMDEF i_MEM_TELEPORTDESLOG]
ID=i_MEMORY
NAME=ARENAX1 MEMORY
TYPE=T_EQ_SCRIPT

ON=@CREATE
TIMER 1

ON=@TIMER
CONT.GO <DEF.LOCAL_SAIRARENA>
CONT.EVENTS -E_ARENA_1X1
CONT.TAG.ARENA_1X1_ITEM
CONT.KILLS=<CONT.TAG0.ARENAX1.KILLS>
CONT.FAME=<CONT.TAG0.ARENAX1.FAME>
CONT.KARMA=<CONT.TAG0.ARENAX1.KARMA>
CONT.TAG0.ARENAX1.KARMA=
CONT.TAG0.ARENAX1.FAME=
CONT.TAG0.ARENAX1.KILLS=
CONT.TAG0.ARENAX1.TEAM=
CONT.CRIMINAL 0
REMOVE
RETURN 1

[EOF]
