[ITEMDEF I_anuncios]
ID=0ed6
NAME=Pedra NOTICIA
WEIGHT=30000

on=@CREATE
Attr=010
TAG0.ANUNCIO 1
TIMER 300

ON=@CLICK
MESSAGE Timer <TIMER>
MESSAGE <NAME>
RETURN 1


ON=@TIMER
IF (<ISEMPTY <VAR.ANOUNCE<dTAG0.ANUNCIO>>>)
	TAG0.ANUNCIO = 1
	IF (<ISEMPTY <VAR.ANOUNCE1>>)
		RETURN 1
	ENDIF
ENDIF

SERV.ALLCLIENTS SYSMESSAGE @0bb7,,1 Anuncio global: <VAR.ANOUNCE<dtag0.anuncio>>
TAG0.ANUNCIO += 1

IF <dTAG0.ANUNCIO> > 3
	TAG0.ANUNCIO = 1
ENDIF

TIMER 240
RETURN 1

ON=@DCLICK
IF <SRC.ACCOUNT.PLEVEL> < 6
	RETURN 1
ENDIF

SRC.CTAG0.PEDRAANUNCIO <UID>
SRC.MENU ANOUNCEMENT0
RETURN 1

[MENU ANOUNCEMENT0]
O que deseja fazer?

ON=0 Adicionar/Editar Anouncement

SRC.MENU ANOUCEMENT1

ON=0 Iniciar ANUNCIOS
F_INICIA_ANUNCIO

ON=0 Parar de anunciar (nao cancela anuncios)
F_CANCELA_ANUNCIO

[MENU ANOUCEMENT1]
Anuncios

ON=0 Anuncio 1 <VAR0.ANOUNCE1>
F_EDITANOUNCE 1

ON=0 Anuncio 2 <VAR0.ANOUNCE2>
IF (<ISEMPTY <VAR.ANOUNCE1>>)
	SRC.SYSMESSAGE Anuncio 1 esta vazio, use o slot 1.
ELSE
	F_EDITANOUNCE 2
ENDIF

ON=0 Anuncio 3 <VAR0.ANOUNCE3>
IF (<ISEMPTY <VAR.ANOUNCE2>>)
	SRC.SYSMESSAGE Anuncio 2 esta vazio, use o slot 2.
ELSE
	F_EDITANOUNCE 3
ENDIF

[FUNCTION F_EDITANOUNCE]
IF <ARGN> == 1
	SRC.PROMPTCONSOLE F_ANNOUNCEADD1 @0481,,1 DIGITE O ANUNCIO QUE DESEJA COLOCAR NO SLOT 1. Use poucas palavras. (Esc para cancelar)
ELIF <ARGN> == 2
	SRC.PROMPTCONSOLE F_ANNOUNCEADD2 @0481,,1 DIGITE O ANUNCIO QUE DESEJA COLOCAR NO SLOT 2. Use poucas palavras. (Esc para cancelar)
ELIF <ARGN> == 3
	SRC.PROMPTCONSOLE F_ANNOUNCEADD3 @0481,,1 DIGITE O ANUNCIO QUE DESEJA COLOCAR NO SLOT 3. Use poucas palavras. (Esc para cancelar)
ENDIF

[FUNCTION F_ANNOUNCEADD1]
	VAR0.ANOUNCE1 <ARGS>
	SRC.SYSMESSAGE Anuncio setado para <VAR0.ANOUNCE1>
[FUNCTION F_ANNOUNCEADD2]
	VAR0.ANOUNCE2 <ARGS>
	SRC.SYSMESSAGE Anuncio setado para <VAR0.ANOUNCE2>
[FUNCTION F_ANNOUNCEADD3]
	VAR0.ANOUNCE3 <ARGS>
	SRC.SYSMESSAGE Anuncio setado para <VAR0.ANOUNCE3>
	
[FUNCTION F_CANCELA_ANUNCIO]
TRY UID.<SRC.CTAG0.PEDRAANUNCIO>.TIMER -1
SRC.SYSMESSAGE ANUNCIOS PAUSADOS.

[FUNCTION F_INICIA_ANUNCIO]
TRY UID.<SRC.CTAG0.PEDRAANUNCIO>.TIMER 300
SRC.SYSMESSAGE ANUNCIOS INICIADOS


[EOF]