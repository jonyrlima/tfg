[ITEMDEF I_BASKET_REAGENT]
ID I_BUCKET // Bucket
NAME Reagent Basket
WEIGHT 5
TYPE T_CONTAINER

ON=@CREATE
ATTR 04
COLOR 0481

ON=@DCLICK
IF (<LINK> == 04fffffff)
 LINK <SRC>
ELSEIF (<LINK> != <SRC>) && (!<SRC.ISGM>)
 CONT <LINK.FINDLAYER.21>
 SRC.SYSMESSAGE @,,1 Este item nao te pertence.
 RETURN 1
ENDIF
IF ((<TOPOBJ> != <SRC>) || (<CONT> != <SRC.FINDLAYER.21>)) && (!<SRC.ISGM>)
 SRC.SYSMESSAGE @,,1 O item deve estar em sua bag.
 RETURN 1
ENDIF
IF (<LINK> == <SRC>)
 ARGS I_REAG_BLACK_PEARL,I_REAG_BLOOD_MOSS,I_REAG_GARLIC,I_REAG_GINSENG,I_REAG_MANDRAKE_ROOT,I_REAG_NIGHTSHADE,I_REAG_SULFUR_ASH,I_REAG_SPIDER_SILK
 FOR 0 <EVAL <ARGV>-1>
  WHILE (<SRC.FINDID.<ARGV[<dLOCAL._FOR>]>>) && (<SRC.FINDID.<ARGV[<dLOCAL._FOR>]>.CONT> != <UID>)
   SRC.FINDID.<ARGV[<dLOCAL._FOR>]>.CONT <UID>
  ENDWHILE
 ENDFOR
ENDIF

ON=@DROPON_SELF
IF (<ARGO.TYPE> != T_REAGENT)
 SRC.SYSMESSAGE @026,,1 Voce so pode colocar reagentes neste container.
 RETURN 1
ENDIF


[EVENTS E_BASKET_REAGENT]
ON=@DEATH
IF <SRC.RESTEST I_BASKET_REAGENT>
 FORCONT <FINDID.I_BASKET_REAGENT>
  CONT <TOPOBJ.FINDLAYER.21>
 ENDFOR
ENDIF