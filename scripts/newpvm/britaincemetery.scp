////////////////////////////////////
///////BRITAIN CEMETERY/////////////


[ITEMDEF I_CEMITA_EVENT_CONTROL]
DEFNAME=I_CEMITA_EVENT_CONTROL
NAME=Stone controladora do cemita (NAO MEXER)
ID=I_GRAVE_STONE_2
TYPE=T_NORMAL

ON=@DCLICK
SRC.SYSMESSAGE SAI FORA!
RETURN 1

ON=@TIMER
TAG.TIMERVEZES -= 1

IF <TAG0.TIMERVEZES> > 0
	REGION.ALLCLIENTS SYSMESSAGE Restam <EVAL <TAG0.TIMERVEZES>*10> minutos para o cemiterio se fechar...
	TIMER 600
ENDIF

IF <TAG0.TIMERVEZES> == 0
	F_FECHAR_CEMITA 15
ENDIF

RETURN 1

[FUNCTION F_FECHAR_CEMITA]
	FORINSTANCES I_GATE_IRON
		IF (<ISEVENT.T_DOOR_CEMITA_scorpion>) || (<ISEVENT.T_DOOR_CEMITA_Coveiro>)
			TAG.LOCK 1
			IF <TIMER> > 1
				TIMERD = 1 //FECHAR PORTAS
			ENDIF
		ELIF (<ISEVENT.T_DOOR_CEMITA>)
			IF <TIMER> > 0
				TIMERD = 1
			ENDIF
			TAG.LOCK 1
			REF69 <UID.<UID>>
			REF69.TIMERF 600, TAG.LOCK
		ENDIF
	ENDFOR
	FORINSTANCES I_CEMITA_EVENT_CONTROL
		TIMER = -1
		REGION.ALLCLIENTS F_GO <R1380,1387>,<R1495,1498>,10
	ENDFOR
	FORITEMS 200
		IF (<TYPE> == T_CORPSE)
			IF !(<AMOUNT> == 400) && !(<AMOUNT> == 401)
				REMOVE
			ENDIF
		ENDIF
		IF (<TYPE> == T_SPAWN_CHAR)
			TIMER -1
		ENDIF
	ENDFOR
	FORCHARS 120
		//IF (<ISPLAYER>) && ((<isevent.E_ON_CEMITA>) || (<ISEVENT.E_ON_COVEIRO>))
		//	F_GO <R1382,1384>,<R1495,1499>,10
		//ENDIF
		IF !(<ISPLAYER>) && ((<ISEVENT.E_TFG_PVM>) || (<ISEVENT.E_TFG_BOSS>))
			REMOVE
		ENDIF
	ENDFOR

	VAR.CEMITA_ATIVADO = 0
	VAR.ScorpionABERTO = 0

[FUNCTION F_ATIVAR_CEMITA]
FORINSTANCES I_CEMITA_EVENT_CONTROL
	VAR.CEMITA_ATIVADO = 1
	TIMER 600
	TAG.TIMERVEZES 6
	F_ATIVAR_CEMITA_2, <UID>
ENDFOR
S @,,1 CEMITA ATIVADO! <UID>
//SERV.B @0771 O Cemiterio de Britain foi ativado!

[FUNCTION F_ATIVAR_CEMITA_2]
REF1 <UID.<ARGS>>
FORITEMS 200 
    IF (<baseid> == I_WORLDGEM_BIT) && (<REGION.UID> == <REF1.REGION.UID>)
       TIMER 1 
    ENDIF 
ENDFOR


[TYPEDEF T_DOOR_CEMITA]
ON=@CLICK
IF <SRC.ISGM>
	MESSAGE @,,1 [Nao mudar de lugar]
ENDIF

ON=@DCLICK
IF <TAG0.LOCK> == 1
	SRC.SYSMESSAGE @,,1 You must wait to activate the graveyard chaos again...
	RETURN 1
ENDIF
IF (<VAR.CEMITA_ATIVADO> == 0)
	F_ATIVAR_CEMITA
ENDIF

[TYPEDEF T_DOOR_CEMITA_scorpion]
ON=@CLICK
IF <SRC.ISGM>
	MESSAGE @,,1 [Nao mudar de lugar]
ENDIF

ON=@DCLICK
IF <TAG0.LOCK> == 1
	SRC.SYSMESSAGE @,,1 This gate is locked by unknown forces...
	RETURN 1
ENDIF

[TYPEDEF T_DOOR_CEMITA_Coveiro]
ON=@CLICK
IF <SRC.ISGM>
	MESSAGE @,,1 [Nao mudar de lugar]
ENDIF

ON=@DCLICK
IF <TAG0.LOCK> == 1
	SRC.SYSMESSAGE @,,1 This gate is locked by unknown forces...
	RETURN 1
ENDIF


[EVENTS E_TFG_PVM]
ON=@DEATHCORPSE
LOCAL.MOBSFOUND <ARGO.F_MOBSCHECK>-1
S <DLOCAL.MOBSFOUND>
IF (<VAR0.ScorpionABERTO> == 0)
	IF (<DLOCAL.MOBSFOUND> > 0)  //ainda tem monstros
		REF1 <UID.<ARGO.MORE2>>
		REF1.SYSMESSAGE @,,1 You have shaken the scorpion gates, but not enough...try harder!
	ELIF (<DLOCAL.MOBSFOUND> == 0) //fim dos monstros
		ARGO.TIMERF 1, F_ABRE_SCORPION_GATE
		VAR.ScorpionABERTO = 1
	ELSE
		S ERRO BRITAIN CEMETERY LN 55
	ENDIF
ENDIF

ON=@GETHIT
IF <REGION.UID> != <SRC.REGION.UID>
	RETURN 1
ENDIF

[FUNCTION F_ABRE_SCORPION_GATE]
FORINSTANCES I_GATE_IRON
	IF <ISEVENT.T_DOOR_CEMITA_scorpion>
		TAG.LOCK
		LOCAL.AMT ++
		IF <DLOCAL.AMT> == 1
			REGION.ALLCLIENTS SYSMESSAGE @,,1 The Scorpion gates are now unlocked for a while...
		ENDIF
	ENDIF
ENDFOR

[EVENTS E_TFG_BOSS]
ON=@GETHIT
if <src.region.uid> != <region.uid>
	return 1
endif

[FUNCTION F_MOBSCHECK]
LOCAL.MOBSFOUND 0
FORCHARS 120
	IF (<ISEVENT.E_TFG_PVM>) && (<SRC.REGION.UID> == <REGION.UID>)
		LOCAL.MOBSFOUND ++
	ENDIF
ENDFOR
RETURN <DLOCAL.MOBSFOUND>

[FUNCTION F_KCX]
LOCAL.MINIONS <ARGV[0]>
LOCAL.MOB <ARGV[1]>

IF <DLOCAL.MINIONS> < 3
	LOCAL.MINIONS 3
ENDIF
SRC.TIMERF 8, TAG.SUMMONHELP
LOCAL.LIVEMINIONS 0
FORCHARS 25
	IF (<ISEVENT.E_TFG_PVM>)
		LOCAL.LIVEMINIONS ++
	ENDIF
ENDFOR
LOCAL.MINIONS = <EVAL <LOCAL.MINIONS>-<LOCAL.LIVEMINIONS>>
IF <DLOCAL.MINIONS> < 1
	LOCAL.MINIONS 1
ENDIF
FOR 1 <DLOCAL.MINIONS>
	SERV.NEWNPC <local.mob>
	NEW.COLOR 0771
	NEW.NAME <SRC.name> Servant
	NEW.P <SRC.P>
ENDFOR

[FUNCTION F_ABRE_GATE_COVEIRO]
FORINSTANCES I_GATE_IRON
	IF <ISEVENT.T_DOOR_CEMITA_Coveiro>
		FORCHARS 50
			IF <BASEID> == C_COVEIRO 
				INVUL 0
			ENDIF
		ENDFOR
		TAG.LOCK
		LOCAL.AMT ++
		IF <DLOCAL.AMT> == 1
			REGION.ALLCLIENTS SYSMESSAGE @,,1 Grave Digger's gates are now open! Be careful...//'
		ENDIF
	ENDIF
ENDFOR

[REGIONTYPE r_CEMITA]
DEFNAME=r_CEMITA

ON=@ENTER
SRC.EVENTS +E_ON_CEMITA

ON=@EXIT
SRC.EVENTS -E_ON_CEMITA

[REGIONTYPE r_CEMITA_1]
DEFNAME=r_CEMITA_1

ON=@ENTER
SRC.EVENTS +E_ON_COVEIRO

ON=@EXIT
SRC.EVENTS -E_ON_COVEIRO

[EVENTS E_ON_COVEIRO]

[EVENTS E_ON_CEMITA]


