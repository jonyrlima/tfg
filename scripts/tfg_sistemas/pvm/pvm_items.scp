[TYPEDEF t_pvm_weapon]
ON=@Click
	MESSAGE @<COLOR>,,1 <NAME>
	RETURN 1

[ITEMDEF i_paralyze_disable_effect]
NAME=Paralyze Disable Effect
ID=i_memory
TYPE=t_eq_script

ON=@Create
	LINK=<SRC.UID>

ON=@Timer
	RETURN 0

[FUNCTION f_make_pvm_weapon]
IF (<ARGV> != 2)
	RETURN 1
ENDIF

REF1 <ARGV[0]>
LOCAL.EFFECT <ARGV[1]>
REF1.EVENTS +t_pvm_weapon
REF1.ATTR |= attr_magic

IF (<LOCAL.EFFECT> == s_flamestrike)
	REF1.COLOR 0793
	REF1.NAME Fire <REF1.NAME>

	REF1.TAG.LEGENDARY_WEAPON_EFFECT s_flamestrike
	REF1.TAG.LEGENDARY_WEAPON_EFFECT_CHANCE <EVAL {11 4 9 2 7 1}>

	IF (<REF1.TAG.LEGENDARY_WEAPON_EFFECT_CHANCE> == 11)
		REF1.TIMERF 3,HITPOINTS=200
		REF1.NAME Rare <REF1.NAME>
	ELSEIF (<REF1.TAG.LEGENDARY_WEAPON_EFFECT_CHANCE> == 9)
		REF1.TIMERF 3,HITPOINTS=270
		REF1.NAME Legendary <REF1.NAME>
	ELSEIF (<REF1.TAG.LEGENDARY_WEAPON_EFFECT_CHANCE> == 7)
		REF1.TIMERF 3,HITPOINTS=340
		REF1.NAME Exotic <REF1.NAME>
	ENDIF
ELSEIF (<LOCAL.EFFECT> == s_lightning)
	REF1.COLOR 07c9
	REF1.NAME Lightning <REF1.NAME>

	REF1.TAG.LEGENDARY_WEAPON_EFFECT s_lightning
	REF1.TAG.LEGENDARY_WEAPON_EFFECT_CHANCE <EVAL {8 4 6 2 4 1}>

	IF (<REF1.TAG.LEGENDARY_WEAPON_EFFECT_CHANCE> == 8)
		REF1.TIMERF 3,HITPOINTS=200
		REF1.NAME Rare <REF1.NAME>
	ELSEIF (<REF1.TAG.LEGENDARY_WEAPON_EFFECT_CHANCE> == 6)
		REF1.TIMERF 3,HITPOINTS=270
		REF1.NAME Legendary <REF1.NAME>
	ELSEIF (<REF1.TAG.LEGENDARY_WEAPON_EFFECT_CHANCE> == 4)
		REF1.TIMERF 3,HITPOINTS=340
		REF1.NAME Exotic <REF1.NAME>
	ENDIF
ELSEIF (<LOCAL.EFFECT> == s_paralyze)
	REF1.COLOR color_ice
	REF1.NAME Frost <REF1.NAME>

	REF1.TAG.LEGENDARY_WEAPON_EFFECT s_paralyze
	REF1.TAG.LEGENDARY_WEAPON_EFFECT_CHANCE <EVAL {17 4 14 2 11 1}>

	IF (<REF1.TAG.LEGENDARY_WEAPON_EFFECT_CHANCE> == 17)
		REF1.TIMERF 3,HITPOINTS=200
		REF1.NAME Rare <REF1.NAME>
	ELSEIF (<REF1.TAG.LEGENDARY_WEAPON_EFFECT_CHANCE> == 14)
		REF1.TIMERF 3,HITPOINTS=270
		REF1.NAME Legendary <REF1.NAME>
	ELSEIF (<REF1.TAG.LEGENDARY_WEAPON_EFFECT_CHANCE> == 11)
		REF1.TIMERF 3,HITPOINTS=340
		REF1.NAME Exotic <REF1.NAME>
	ENDIF
ENDIF

[DEFNAME magic_gem_settings]
MAGIC_GEMS_MAX 15
MAGIC_GEM_1_DISPID i_virtstone_4
MAGIC_GEM_1_COLOR 00
MAGIC_GEM_1_NAME Wisdom

MAGIC_GEM_2_DISPID i_virtstone_8
MAGIC_GEM_2_COLOR 07da
MAGIC_GEM_2_NAME Greed

MAGIC_GEM_3_DISPID i_virtstone_4
MAGIC_GEM_3_COLOR 0769
MAGIC_GEM_3_NAME Luxury

MAGIC_GEM_4_DISPID i_virtstone_4
MAGIC_GEM_4_COLOR 012c
MAGIC_GEM_4_NAME Haste

MAGIC_GEM_5_DISPID i_virtstone_3
MAGIC_GEM_5_COLOR 085
MAGIC_GEM_5_NAME Power

MAGIC_GEM_6_DISPID i_virtstone_6
MAGIC_GEM_6_COLOR 04c2
MAGIC_GEM_6_NAME Nature

MAGIC_GEM_7_DISPID i_virtstone_6
MAGIC_GEM_7_COLOR 0172
MAGIC_GEM_7_NAME Faith

MAGIC_GEM_8_DISPID i_virtstone_6
MAGIC_GEM_8_COLOR 0100
MAGIC_GEM_8_NAME Wrath

MAGIC_GEM_9_DISPID i_virtstone_6
MAGIC_GEM_9_COLOR 00
MAGIC_GEM_9_NAME Blood

MAGIC_GEM_10_DISPID i_virtstone_5
MAGIC_GEM_10_COLOR 05e7
MAGIC_GEM_10_NAME Peace

MAGIC_GEM_11_DISPID i_virtstone_5
MAGIC_GEM_11_COLOR 045a
MAGIC_GEM_11_NAME Life

MAGIC_GEM_12_DISPID i_virtstone_5
MAGIC_GEM_12_COLOR 032c
MAGIC_GEM_12_NAME Death

MAGIC_GEM_13_DISPID i_virtstone_5
MAGIC_GEM_13_COLOR 01eb
MAGIC_GEM_13_NAME Energy

MAGIC_GEM_14_DISPID i_virtstone_5
MAGIC_GEM_14_COLOR 01d
MAGIC_GEM_14_NAME Hunger

MAGIC_GEM_15_DISPID i_virtstone_5
MAGIC_GEM_15_COLOR 00
MAGIC_GEM_15_NAME Soul

[ITEMDEF i_magic_gem]
NAME=Magic Gem
ID=i_virtstone_1
TYPE=t_normal
// avoid stack
CAN=0

ON=@Create
	LOCAL.RANDOM <R1,<DDEF.MAGIC_GEMS_MAX>>

	DISPID <DEF.MAGIC_GEM_<DLOCAL.RANDOM>_DISPID>
	COLOR <DEF.MAGIC_GEM_<DLOCAL.RANDOM>_COLOR>
	NAME <DEF.MAGIC_GEM_<DLOCAL.RANDOM>_NAME> <NAME>

ON=@DClick
	SRC.SYSMESSAGE @,,1 You must get <DDEF.MAGIC_GEMS_MAX> gems to earn incredible prizes !
    RETURN 1

[FUNCTION f_fix_magic_gem_names]
FORINSTANCES i_magic_gem
	FOR 1 15
		IF (<DISPID> == <DEF.MAGIC_GEM_<DLOCAL._FOR>_DISPID>) && (<COLOR> == <DEF.MAGIC_GEM_<DLOCAL._FOR>_COLOR>)
			NAME <DEF.MAGIC_GEM_<DLOCAL._FOR>_NAME> <SERV.ITEMDEF.<BASEID>.NAME>
		ENDIF
	ENDFOR
ENDFOR

[FUNCTION f_handle_pvm_level_item_hits]
IF (<ARGV> != 2)
	RETURN 1
ENDIF

REF1 <ARGV[0]>
LOCAL.HITS <ARGV[1]>

IF (<REF1.ISTEVENT.t_pvm_level_item>)
	IF (<REF1.TAG.LEVEL> >= <REF1.TAG.MAXIMUM_LEVEL>)
		RETURN 2
	ENDIF

	IF (!<REF1.TAG0.LEVEL_UP_EXPERIENCE>)
		REF1.TAG.LEVEL_UP_EXPERIENCE <EVAL <REF1.TAG.LEVEL_INITIAL_EXPERIENCE> + (<REF1.TAG.LEVEL_UP_INCREASE> * (<REF1.TAG.LEVEL> - 1))>
	ENDIF

	REF1.TAG0.LEVEL_EXPERIENCE += <LOCAL.HITS>

	IF (<REF1.TAG.LEVEL_EXPERIENCE> >= <REF1.TAG.LEVEL_UP_EXPERIENCE>)
		REF1.TAG0.LEVEL_EXPERIENCE <EVAL <REF1.TAG.LEVEL_EXPERIENCE> - <REF1.TAG.LEVEL_UP_EXPERIENCE>>

		IF (<REF1.TAG.LEVEL> < <REF1.TAG.MAXIMUM_LEVEL>)
			REF1.TAG.LEVEL += 1
			REF1.TAG.LEVEL_ATTRIBUTE_BONUS <EVAL <REF1.TAG.LEVEL> * <REF1.TAG.LEVEL_BONUS_ATTRIBUTE_INCREASE>>
			REF1.MAXHITS <EVAL <REF1.TAG.LEVEL_INITIAL_HITPOINTS> + (<REF1.TAG.LEVEL_HITPOINTS_INCREASE> * (<REF1.TAG.LEVEL> - 1))>
			REF1.HITS <REF1.MAXHITS>
			REF1.TAG.LEVEL_UP_EXPERIENCE
		ELSE
			REF1.TAG0.LEVEL_EXPERIENCE
			REF1.TAG0.LEVEL_UP_EXPERIENCE
		ENDIF

		SRC.SYSMESSAGE @,,1 Congratulations, <REF1.NAME> has been leveled up to <DREF1.TAG.LEVEL> ! The item durability has been restored !
	ENDIF
ENDIF

[TYPEDEF t_pvm_level_item]
ON=@Create
	TIMERF 2, HITPOINTS <TAG.LEVEL_INITIAL_HITPOINTS>
	TAG.LEVEL 1
	TAG.LEVEL_ATTRIBUTE_BONUS <EVAL <TAG.LEVEL> * <TAG.LEVEL_BONUS_ATTRIBUTE_INCREASE>>

ON=@Click
	MESSAGE @90 [<TAG.LEVEL_ATTRIBUTE_DESCRIPTION>: <d<TAG.LEVEL_ATTRIBUTE>> +<dTAG.LEVEL_ATTRIBUTE_BONUS>]
	MESSAGE @,,1 <NAME> (+<dTAG.LEVEL>)

	IF (<SRC.UID> == <LINK>) && (<TAG0.LEVEL_UP_EXPERIENCE>) && (<TAG.LEVEL> < <TAG.MAXIMUM_LEVEL>)
		MESSAGE @,,1 [Experience: <EVAL (<TAG.LEVEL_EXPERIENCE> * 100) / <TAG.LEVEL_UP_EXPERIENCE>>%]
	ENDIF

	RETURN 1

// temporary
ON=@Equip
	HITS <MAXHITS>

[ITEMDEF i_shield_of_the_defender]
ID=i_shield_order
NAME=Shield of the Defender
TYPE=t_shield
FLIP=1
WEIGHT=10
ARMOR=20
REQSTR=100
TEVENTS=t_global_bound_item,t_pvm_level_item,t_safe_drop
TAG.LEVEL_ATTRIBUTE=ARMOR
TAG.LEVEL_ATTRIBUTE_DESCRIPTION=Armor
TAG.LEVEL_BONUS_ATTRIBUTE_INCREASE=1
TAG.LEVEL_INITIAL_HITPOINTS=500
TAG.LEVEL_HITPOINTS_INCREASE=100
TAG.MAXIMUM_LEVEL=15
TAG.LEVEL_INITIAL_EXPERIENCE=10000
TAG.LEVEL_UP_INCREASE=15000

CATEGORY=Armas Especiais
SUBSECTION=Escudos
DESCRIPTION=Shield of the Defender

ON=@Create
	COLOR=0a48

[ITEMDEF i_axe_of_the_slayer]
ID=i_axe
NAME=Axe of the Slayer
WEIGHT=8
TYPE=T_WEAPON_AXE
FLIP=1
DAM=17,23
SPEED=57
SKILL=Swordsmanship
REQSTR=100
TWOHANDS=Y
TEVENTS=t_global_bound_item,t_pvm_level_item,t_safe_drop
TAG.LEVEL_ATTRIBUTE=DAMAGE
TAG.LEVEL_ATTRIBUTE_DESCRIPTION=Damage
TAG.LEVEL_BONUS_ATTRIBUTE_INCREASE=2
TAG.LEVEL_INITIAL_HITPOINTS=500
TAG.LEVEL_HITPOINTS_INCREASE=100
TAG.MAXIMUM_LEVEL=15
TAG.LEVEL_INITIAL_EXPERIENCE=12000
TAG.LEVEL_UP_INCREASE=17000

ON=@Create
	COLOR=0b9f

ON=@EQUIP
	LINK <SRC.UID>
	SRC.EVENTS +e_pb_mana

ON=@UNEQUIP
	SRC.EVENTS -e_pb_mana

[ITEMDEF i_bow_of_the_destroyer]
ID=i_bow
NAME=Bow of the Destroyer
TYPE=T_WEAPON_BOW
FLIP=1
WEIGHT=3
RANGE=2,12
DAM=27,31
SPEED=70
SKILL=Archery
TWOHANDS=Y
TEVENTS=t_global_bound_item,t_pvm_level_item,t_safe_drop
TAG.LEVEL_ATTRIBUTE=DAMAGE
TAG.LEVEL_ATTRIBUTE_DESCRIPTION=Damage
TAG.LEVEL_BONUS_ATTRIBUTE_INCREASE=1
TAG.LEVEL_INITIAL_HITPOINTS=500
TAG.LEVEL_HITPOINTS_INCREASE=100
TAG.MAXIMUM_LEVEL=15
TAG.LEVEL_INITIAL_EXPERIENCE=12000
TAG.LEVEL_UP_INCREASE=17000

ON=@Create
	COLOR=0a25

[ITEMDEF i_runic_stone]
NAME=Runic Stone
ID=i_arcane_focus
TYPE=t_normal
// avoid stack
CAN=0

ON=@Create
	TIMER <DEF.PAROXYSMUS_PALACE_RUNIC_STONE_TIMER>

ON=@Click
	IF (<TOPOBJ.UID> == <SRC.UID>)
		MESSAGE @,,1 [Remaining time: <TIMEFORMAT 0,<TIMER>>]
	ENDIF

ON=@Timer
	RETURN 0

[ITEMDEF i_magic_gem_item_deed]
ID=i_deed
NAME=Magic Gem Item Deed
VALUE=40

ON=@Create
	COLOR=2045
	ATTR=attr_newbie

ON=@DClick
    IF (<TOPOBJ.UID> != <SRC.UID>)
		SRC.SYSMESSAGE @021 The deed must be in your backpack.
		RETURN 1
	ENDIF

 	SRC.CTAG.MAGIC_GEM_ITEM_DEED <UID>
	SRC.MENU MENU_MAGIC_GEM_ITEM

	RETURN 1

[MENU MENU_MAGIC_GEM_ITEM]
Please select the magic item you want to use. Choose wisely because you can't change it !

ON=0 Bow of the Destroyer
	SERV.NEWITEM i_bow_of_the_destroyer
	NEW.LINK <SRC.UID>
	NEW.ATTR attr_newbie
	NEW.BOUNCE

	SERV.LOG <NEW.UID> / <NEW.NAME> has been generated for <SRC.NAME>.

	UID.<SRC.CTAG.MAGIC_GEM_ITEM_DEED>.REMOVE

ON=0 Axe of the Slayer
	SERV.NEWITEM i_axe_of_the_slayer
	NEW.LINK <SRC.UID>
	NEW.ATTR attr_newbie
	NEW.BOUNCE

	SERV.LOG <NEW.UID> / <NEW.NAME> has been generated for <SRC.NAME>.

	UID.<SRC.CTAG.MAGIC_GEM_ITEM_DEED>.REMOVE

ON=0 Shield of the Defender
	SERV.NEWITEM i_shield_of_the_defender
	NEW.LINK <SRC.UID>
	NEW.ATTR attr_newbie
	NEW.BOUNCE

	SERV.LOG <NEW.UID> / <NEW.NAME> has been generated for <SRC.NAME>.

	UID.<SRC.CTAG.MAGIC_GEM_ITEM_DEED>.REMOVE

[ITEMDEF i_paroxysmus_dinner]
ID=i_rabbit_spitted
NAME=Paroxysmus' Dinner
TYPE=t_meat_raw
FLIP=1
WEIGHT=2.7
RESOURCES=1 i_rabbit_skinned,2 i_spit
CATEGORY=Items by Professions
SUBSECTION=Cook
DESCRIPTION=Rabbit on a Spit
DUPELIST=01e98

[EOF]