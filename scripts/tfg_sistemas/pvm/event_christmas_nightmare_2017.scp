[DEFNAME CHRISTMAS_NIGHTMARE_2017_SETTINGS]
CHRISTMAS_NIGHTMARE_2017_COLOR_1 079c
CHRISTMAS_NIGHTMARE_2017_COLOR_2 07b2
CHRISTMAS_NIGHTMARE_2017_COLOR_COUNT 2

[ITEMDEF i_christmas_nightmare_2017_jin_baori]
ID=i_jinbaori
TYPE=t_clothing
LAYER=layer_ears
NAME=Christmas Nightmare 2017 1st Place Award Jin-Baori
TEVENTS=t_global_bound_item

ON=@Create
	COLOR=<DEF.CHRISTMAS_NIGHTMARE_2017_COLOR_<R1,<DEF.CHRISTMAS_NIGHTMARE_2017_COLOR_COUNT>>>
	ATTR attr_newbie

[ITEMDEF i_christmas_nightmare_2017_ethereal_mount_talisman]
ID=i_talisman4
NAME=Christmas Nightmare 2017 Ethereal Mount Talisman
TEVENTS=t_global_bound_item

ON=@Create
	ATTR=attr_newbie
	COLOR=<DEF.CHRISTMAS_NIGHTMARE_2017_COLOR_<R1,<DEF.CHRISTMAS_NIGHTMARE_2017_COLOR_COUNT>>>

[ITEMDEF i_christmas_nightmare_2017_bottle_hair_dye]
ID=i_bottle_hair_dye
NAME=Christmas Nightmare 2017 Hair Dye

ON=@Create
	ATTR=attr_newbie
	COLOR=<DEF.CHRISTMAS_NIGHTMARE_2017_COLOR_<R1,<DEF.CHRISTMAS_NIGHTMARE_2017_COLOR_COUNT>>>

ON=@Click
	MESSAGE @<f_get_christmas_nightmare_2017_text_color <COLOR>>,,1 <NAME>
	RETURN 1

ON=@DClick
	IF (<TOPOBJ.UID> != <SRC.UID>)
		SRC.SYSMESSAGE @,,1 This item must be inside your backpack or bank to be used.
		RETURN 1
	ENDIF

	IF (<SRC.FINDLAYER.layer_hair.UID>)
		SRC.FINDLAYER.layer_hair.COLOR <COLOR>
		REMOVE
	ELSE
		SRC.SYSMESSAGE @,,1 <DEFMSG.itemuse_dye_nohair>
	ENDIF

	RETURN 1

[ITEMDEF i_christmas_nightmare_2017_title_deed]
ID=i_deed
NAME=Christmas Nightmare 2017 Title Deed

ON=@Create
	COLOR = <DEF.CHRISTMAS_NIGHTMARE_2017_COLOR_<R1,<DEF.CHRISTMAS_NIGHTMARE_2017_COLOR_COUNT>>>
	DORAND 16
		TAG.CHRISTMAS_NIGHTMARE_2017_TITLE_NAME Santa Claus
		TAG.CHRISTMAS_NIGHTMARE_2017_TITLE_NAME Mrs Claus
		TAG.CHRISTMAS_NIGHTMARE_2017_TITLE_NAME Krampus
		TAG.CHRISTMAS_NIGHTMARE_2017_TITLE_NAME The Grinch
		TAG.CHRISTMAS_NIGHTMARE_2017_TITLE_NAME The Scrooge
		TAG.CHRISTMAS_NIGHTMARE_2017_TITLE_NAME Jack Skellington
		TAG.CHRISTMAS_NIGHTMARE_2017_TITLE_NAME Rudolph, The Reindeer
		TAG.CHRISTMAS_NIGHTMARE_2017_TITLE_NAME Dasher, The Reindeer
		TAG.CHRISTMAS_NIGHTMARE_2017_TITLE_NAME Dancer, The Reindeer
		TAG.CHRISTMAS_NIGHTMARE_2017_TITLE_NAME Prancer, The Reindeer
		TAG.CHRISTMAS_NIGHTMARE_2017_TITLE_NAME Vixen, The Reindeer
		TAG.CHRISTMAS_NIGHTMARE_2017_TITLE_NAME Comet, The Reindeer
		TAG.CHRISTMAS_NIGHTMARE_2017_TITLE_NAME Cupid, The Reindeer
		TAG.CHRISTMAS_NIGHTMARE_2017_TITLE_NAME Donner, The Reindeer
		TAG.CHRISTMAS_NIGHTMARE_2017_TITLE_NAME Blitzen, The Reindeer
		TAG.CHRISTMAS_NIGHTMARE_2017_TITLE_NAME Olive, The Reindeer
	ENDDO

ON=@Click
	MESSAGE @<f_get_christmas_nightmare_2017_text_color <COLOR>>,,1 [<TAG.CHRISTMAS_NIGHTMARE_2017_TITLE_NAME>]

ON=@DClick
	IF (<TOPOBJ.UID> != <SRC.UID>)
		SRC.SYSMESSAGE This item must be inside your backpack or bank to be used.
		RETURN 1
	ENDIF

	remove_title_halloween_2017

	SRC.EVENTS +e_christmas_nightmare_2017_title
	SRC.TAG.CHRISTMAS_NIGHTMARE_2017_TITLE_NAME <TAG.CHRISTMAS_NIGHTMARE_2017_TITLE_NAME>
	SRC.TAG.CHRISTMAS_NIGHTMARE_2017_TITLE_COLOR <COLOR>
	REMOVE

	RETURN 1

[ITEMDEF i_christmas_nightmare_2017_dye_tub]
ID=0fab
NAME=Christmas Nightmare 2017 Dye Tub
TYPE=T_NORMAL
VALUE=20

ON=@Create
	MOREX = 1
	COLOR = <DEF.CHRISTMAS_NIGHTMARE_2017_COLOR_<R1,<DEF.CHRISTMAS_NIGHTMARE_2017_COLOR_COUNT>>>

ON=@Click
	MESSAGE @,,1 <QVAL (<MOREX> < 2) ? [<DMOREX> charge] : [<DMOREX> charges] >

ON=@DClick
IF (<ATTR> == attr_move_never)
	SRC.SYSMESSAGE @,,1 This item is locked.
	RETURN 1
ELSEIF (<MOREX> < 1)
	REMOVE
	RETURN 1
ELSE
	TARGET @,,1 Select what you want to dye.
ENDIF
RETURN 1

ON=@Targon_Item
IF (<ARGO.UID> == <UID>)
	SRC.SYSMESSAGE @,,1 This dye tub is already colored.
	RETURN 1
ENDIF

IF (<ARGO.BASEID> == <BASEID>) && (<ARGO.COLOR> == <COLOR>)
  ARGO.MOREX += <MOREX>
  SRC.SYSMESSAGE @,,1 You merged both dye tubs.
  REMOVE
  RETURN 1
ENDIF

if (<ARGO.BASEID> == i_dye_tub) && !(<ARGO.COLOR>)
	if (<ARGO.CONT> == <SRC.FINDLAYER(21).UID>)
		ARGO.REMOVE
		SERV.NEWITEM i_christmas_nightmare_2017_dye_tub
		NEW.COLOR <COLOR>
		NEW.MOREX = 1
		NEW.LINK <SRC>
		SRC.NEW.BOUNCE

		SRC.SYSMESSAGE @,,1 You added one charge to this blank dye tube.

		MOREX -= 1

		IF (<MOREX> <= 0) || (<MOREX> > 100)
			REMOVE
		ENDIF

		RETURN 1
	ELSE
		SRC.SYSMESSAGE @,,1 The dye tub must be inside your bag.
	ENDIF
ENDIF

LOCAL.CHARGE_COST 1

IF (<MOREX> <= 0) || (<MOREX> > 100)
	REMOVE
	RETURN 1
ELSEIF (<ARGO.TYPE> != t_clothing)
	SRC.SYSMESSAGE @,,1 You can't dye this item.
	RETURN 1
ELSEIF (<ARGO.ATTR> & attr_newbie) && !(STRMATCH(i_paragon_*, <ARGO.BASEID>))
	LOCAL.CHARGE_COST 4
ENDIF

IF (<LOCAL.CHARGE_COST> > <MOREX>)
	SRC.SYSMESSAGE @,,1 You must have <DLOCAL.CHARGE_COST> charges to dye this item.
	RETURN 1
ENDIF

IF (<ARGO.TOPOBJ> == <SRC.UID>)
	ARGO.COLOR <COLOR>
	MOREX -= <LOCAL.CHARGE_COST>
	IF (<MOREX> <= 0) || (<MOREX> > 100)
		REMOVE
	ENDIF

	RETURN 1
ELSE
	SRC.SYSMESSAGE @,,1 The item to be dyed must be inside your backpack or bank.
	RETURN 1
ENDIF

ON=@TargOn_Char
	RETURN 1


[FUNCTION f_become_christmas_nightmare_2017_monster]
IF (<ARGV> != 1)
	RETURN
ENDIF

REF1 <ARGS>
REF1.COLOR <DEF.CHRISTMAS_NIGHTMARE_2017_COLOR_<R1,<DEF.CHRISTMAS_NIGHTMARE_2017_COLOR_COUNT>>>
REF1.EVENTS +e_christmas_nightmare_2017
REF1.STR <EVAL (<REF1.STR> * 12) / 10>
REF1.MAXHITS <EVAL (<REF1.MAXHITS> * 12) / 10>
REF1.HITS <REF1.MAXHITS>
REF1.TAG.DISABLE_DEFAULT_LOOT 1

[FUNCTION f_christmas_nightmare_2017_decision]
LOCAL.CHANCE 7

IF (<STR> > 500)
	LOCAL.CHANCE *= 3
ENDIF

IF !(<R<LOCAL.CHANCE>>)
	SERV.NEWNPC <BASEID>
	NEW.P <P>
	NEW.HOME <NEW.P>
	NEW.HOMEDIST 6

	f_become_christmas_nightmare_2017_monster <NEW.UID>

	SERV.LOG A new <NEW.NAME> Christmas Nightmare 2017 monster has been spawned at <NEW.P> (<NEW.REGION.NAME>) !
ENDIF

[FUNCTION f_get_christmas_nightmare_2017_text_color]
IF (<ARGS> == <DEF.CHRISTMAS_NIGHTMARE_2017_COLOR_1>)
	RETURN 021
ELSE
	RETURN 0481
ENDIF

[PLEVEL 1]
score_christmas_nightmare_2017
remove_title_christmas_nightmare_2017

[FUNCTION remove_title_christmas_nightmare_2017]
SRC.EVENTS -e_christmas_nightmare_2017_title
SRC.TAG.CHRISTMAS_NIGHTMARE_2017_TITLE_NAME
SRC.TAG.CHRISTMAS_NIGHTMARE_2017_TITLE_COLOR

[EVENTS e_christmas_nightmare_2017_title]
ON=@Click
	MESSAGE @<f_get_christmas_nightmare_2017_text_color <TAG.CHRISTMAS_NIGHTMARE_2017_TITLE_COLOR>>,,1 [<TAG.CHRISTMAS_NIGHTMARE_2017_TITLE_NAME>]

[FUNCTION score_christmas_nightmare_2017]
SRC.SYSMESSAGE @,,1 Monsters killed in Christmas Nightmare 2017 event: <SRC.DTAG0.CHRISTMAS_NIGHTMARE_2017_MONSTERS_KILLED>

[EVENTS e_christmas_nightmare_2017]
ON=@Click
	MESSAGE @<f_get_christmas_nightmare_2017_text_color <COLOR>>,,1 [Christmas Nightmare]

ON=@DeathCorpse
	ARGO.TAG.DISABLE_CUT 1

ON=@Death
//	IF (<ATTACKER>)
//		FOR 0 <EVAL <ATTACKER> - 1>
//			REF1 <ATTACKER.<DLOCAL._FOR>>
//			IF (<REF1.ISPLAYER>)
//				REF1.TAG0.CHRISTMAS_NIGHTMARE_2017_MONSTERS_KILLED += 1
//			ENDIF
//		ENDFOR
//	ENDIF

	FORCONT <UID>
		REMOVE
	ENDFOR

	LOCAL.CHANCE 144

	IF (<STR> > 500)
		LOCAL.CHANCE /= 4
	ENDIF

	IF !(<R<LOCAL.CHANCE>>)
		SERV.NEWITEM {i_deed_xmas_tree 1 i_christmas_nightmare_2017_dye_tub 3 i_christmas_nightmare_2017_title_deed 7}
		NEW.CONT <UID>
		SERV.LOG New Christmas Nightmare Event 2017 Item dropped: <NEW.NAME>
	ENDIF

	IF (!<R36>)
		NEWITEM <EVAL {i_ethereal_special_mount_deed 3 i_panotrue_g 4 i_panotrue_b 4 i_panotrue_w 4 i_deed_shop_marble_small 1 i_deed_shop_stone_small 1 i_deed_villa_2story 1 i_deed_house_patio_sand 1 i_deed_cabin_log_2story 1 i_deed_house_3room_fence 1 i_Wizardry_ring 2 i_20life_necklace 2 i_20Stamina_necklace 2 i_20Mana_necklace 2 i_paragon_ethereal_mount_talisman 5 i_Titan_ring 4 i_15Stamina_necklace 4 i_Knowledge_ring 4 i_Speedy_ring 4 i_ethereal_mount_deed 7 i_treasure_map_lvl_5 20 I_X_RUNEBOOK 20 i_keg_glb1 20 i_keg_gmb1 20 i_keg_grf1 20 i_chaos_shield 40 i_order_shield 40 i_magic_mask_tribal 50 i_magic_mask_orc 50 i_magic_helm_orc 50 i_magic_mask_deer 50 i_magic_mask_bear 50 i_magic_mask_bear_polar 50 i_magic_mask_bear_black 50 i_power_scroll 60}>
		NEW.ATTR &= ~attr_newbie
		SERV.LOG New Christmas Nightmare Event 2017 Item dropped: <NEW.NAME>
		NEW.CONT <UID>
	ENDIF

	SERV.NEWITEM i_gold
	NEW.AMOUNT <EVAL <BALANCEARGOLD <GRANAMOBS>> * 2>
	NEW.CONT <UID>

[EOF]
